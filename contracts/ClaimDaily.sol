/**
 *Submitted for verification at BscScan.com on 2021-11-11
*/

/**
 *Submitted for verification at BscScan.com on 2021-11-07
*/

/**
 *Submitted for verification at BscScan.com on 2021-10-05
*/

/**
 *Submitted for verification at BscScan.com on 2021-09-20
*/

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */

// CAUTION
// This version of SafeMath should only be used with Solidity 0.8 or later,
// because it relies on the compiler's built in overflow checks.

/**
 * @dev Wrappers over Solidity's arithmetic operations.
 *
 * NOTE: `SafeMath` is no longer needed starting with Solidity 0.8. The compiler
 * now has built in overflow checking.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the substraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
            // benefit is lost if 'b' is also tested.
            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}

interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _setOwner(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _setOwner(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _setOwner(newOwner);
    }

    function _setOwner(address newOwner) private {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

contract TokenVesting is Ownable {
    using SafeMath for uint256;
    // Info of each user
    struct UserInfo {
        uint256 amount;
        uint256 tokensClaimed;
    }

    // mapping user info by address
    mapping(address => UserInfo) public userInfo;

    IERC20 public token;
    uint256 public firstRelease;
    uint256 public startTime;
    uint256 public totalPeriods;
    uint256 public timePerPeriod;
    uint256 public cliff;
    uint256 public totalTokens;
    uint256 public totalTokensDeposited;

    event TokenClaimed(address _address, uint256 tokensClaimed);
    event VestingFunded(uint256 totalTokens);
    event SetStartTime(uint256 _startTime);
    event AddWhitelistUser(address _address, uint256 _amount);
    event RemoveWhitelistUser(address _address);

    /**
     *@dev constructor
     *@param _token address of BEP20 token
     *@param _firstRelease in percent for percent of total tokens that user will receive at first claim, 100 = 1%
     *@param _startTime moment when releasing the first 20% of the tokens
     *@param _cliff delay (seconds) from _startTime after that monthly vesting starts
     *@param _totalPeriods total amount of vesting periods
     *@param _timePerPeriod time in seconds for every vesting period
     */
    constructor(
        address _token,
        uint256 _firstRelease,
        uint256 _startTime,
        uint256 _cliff,
        uint256 _totalPeriods,
        uint256 _timePerPeriod
    ) {
        require(_token != address(0), "zero address not allowed");
        require(_firstRelease <= 10000, "_firstRelease must less than 10000");
        require(_totalPeriods > 0, "_totalPeriods must greater than 0");
        // require(_startTime > block.timestamp, "_startTime must greater than current time");
        require(_timePerPeriod > 0, "_timePerPeriod must greater than 0");

        token = IERC20(_token);
        firstRelease = _firstRelease;
        startTime = _startTime;
        cliff = _cliff;
        totalPeriods = _totalPeriods;
        timePerPeriod = _timePerPeriod;
    }

    /**
     * @dev function responsible for supplying tokens that will be vested
     * @param _totalTokens amount of tokens that will be supplied to this contract
     */
    function fundVesting(uint256 _totalTokens) public onlyOwner {
        // require(token.allowance(msg.sender, address(this)) == _totalTokens, "Not allow spend token");
        totalTokens = totalTokens.add(_totalTokens);
        totalTokensDeposited = totalTokensDeposited.add(_totalTokens);
        token.transferFrom(msg.sender, address(this), _totalTokens);
        emit VestingFunded(_totalTokens);
    }

    /**
     * @dev function that allows receiver to claim tokens, can be called only by receiver
     */
    function claimTokens() public {
        require(totalTokens > 1000000, "Vesting has not been funded yet");
        address _sender = msg.sender;
        UserInfo storage user = userInfo[_sender];
        require(user.amount > user.tokensClaimed.add(1), "All tokens claimed or not whitelist");
        require(block.timestamp > startTime, "Vesting hasn't started yet");

        uint256 timePassed = block.timestamp.sub(startTime);
        uint256 firstClaim = user.amount.mul(firstRelease).div(10000);
        if (timePassed < cliff) {
            require(user.tokensClaimed == 0, "tokens claimed");
            user.tokensClaimed = user.tokensClaimed.add(firstClaim);
            totalTokens = totalTokens.sub(firstClaim);
            token.transfer(_sender, firstClaim);
            emit TokenClaimed(_sender, firstClaim);
        } else {
            timePassed = timePassed.sub(cliff);
            uint256 time = timePassed.div(timePerPeriod).add(1);
            if (time > totalPeriods) {
                time = totalPeriods;
            }

            uint256 _amount = user.amount.sub(firstClaim);
            uint256 tokensToClaim = _amount.mul(time).div(totalPeriods).add(firstClaim).sub(user.tokensClaimed);
            require(tokensToClaim > 0,'Invalid amount');
            
            user.tokensClaimed = user.tokensClaimed.add(tokensToClaim);
            totalTokens = totalTokens.sub(tokensToClaim);
            token.transfer(_sender, tokensToClaim);
            emit TokenClaimed(_sender, tokensToClaim);
        }
    }
    
    function claimAble(address _sender) external view returns(uint256) {
        uint256 amtToClaim;
        UserInfo storage user = userInfo[_sender];
        if (
            user.amount < user.tokensClaimed.add(1)
            || block.timestamp < startTime
            || totalTokens < 1000000) {
            return 0;
        }

        uint256 timePassed = block.timestamp.sub(startTime);
        uint256 firstClaim = user.amount.mul(firstRelease).div(10000);
        if (timePassed < cliff) {
            if (user.tokensClaimed == 0) {
                amtToClaim = firstClaim;
            }
        } else {
            timePassed = timePassed.sub(cliff);
            uint256 time = timePassed.div(timePerPeriod).add(1);
            if (time > totalPeriods) {
                time = totalPeriods;
            }

            uint256 _amount = user.amount.sub(firstClaim);
            amtToClaim = _amount.mul(time).div(totalPeriods).add(firstClaim).sub(user.tokensClaimed);

        }
        
        return  amtToClaim;
    }

    function setStartTime(uint256 _startTime) public onlyOwner {
        startTime = _startTime;

        emit SetStartTime(_startTime);
    }

    function addWhitelistUser(address _address, uint256 _amount) public onlyOwner {
        // require(block.timestamp < startTime, "Vesting has started, cannot add whitelist");
        UserInfo storage user = userInfo[_address];
        user.amount = _amount;
        user.tokensClaimed = 0;

        emit AddWhitelistUser(_address, _amount);
    }

    function removeWhitelistUser(address _address) public onlyOwner {
        delete userInfo[_address];

        emit RemoveWhitelistUser(_address);
    }
    
    
      function addWhitelistMulti(
        address[] memory addrs, 
        uint256[] memory amounts
      ) 
        external 
        onlyOwner
      {  
        require(addrs.length == amounts.length,'Invalid data');
        for(uint i = 0; i < addrs.length; i++){
            UserInfo storage user = userInfo[addrs[i]];
            user.amount = amounts[i];
            user.tokensClaimed = 0;
        }
      }
      
      function updateWhitelistAmount(
        address addr, 
        uint256 amt
      ) 
        external 
        onlyOwner
      {  
        UserInfo storage user = userInfo[addr];
        user.amount = amt;
        user.tokensClaimed = 0;
      }
    
    /* ========== EMERGENCY ========== */
  /*
    Users make mistake by transfering usdt/usdt ... to contract address. 
    This function allows contract owner to withdraw those tokens and send back to users.
    */
  function rescueStuckErc20(address _token) external onlyOwner {
    uint256 _amount = IERC20(_token).balanceOf(address(this));
    IERC20(_token).transfer(owner(), _amount);
  }
}
